/**
 * @file start.S
 *
 * Initialization code for Embedded Xinu on the Raspberry Pi 3.
 */

/* Embedded Xinu, Copyright (C) 2013.  All rights reserved. */


#include <arm.h>     /* For ARM_MODE_SYS */

#define NULLSTK 8192

.section .init
	.globl _start

	/* _start:  Entry point of the Xinu kernel.  This will be the very first
	 * byte of the kernel image and on the Raspberry Pi 3 will be loaded by
	 * the GPU at address 0x80000.  */
	.func _start
_start:
	/* Vector table this is a hack so it is at a known address */
	b	skip
	b	hang
	b	hang
	b	hang
	b	hang
	b	hang
	b	hang
	b	hang
.balign 128
	b	hang
.balign 128
	b	hang
.balign 128
	b	hang
.balign 128
	b	hang
.balign 128
	b	irq_handler
.balign 128
	b	hang
.balign 128
	b	hang
.balign	128
	b	hang
.balign	128
	b	irq_handler


hang:
	b	hang

skip:
	// isolate core 0
	mrs	x7, mpidr_el1
	mov	x8, #0xC1000000
	bic	x8, x7, x8
	cbz	x8, zero
not_zero:
	wfi
	b	not_zero

zero:
	/* Save the pointer to the atags (ARM boot tags).  The bootloader should
	 * pass this in x0, but override 0 with 0x100 to deal with old
	 * bootloaders that assume Linux's default behavior.  (This is the case
	 * with, for example, the bootloader on the BCM2837.)  */
	ldr 	x3, =atags_ptr
	str 	x0, [x3]

	/* Set to the correct Exception Level for processor */
	bl 	el_setter

	/* Continue excecution at reset_handler */
	b	reset_handler
	.endfunc

/* ARMv8 exception vector table. */
_vectors:
	/* Current EL with SP0 */
	b		sync_addr	/* Synchronous */
	.balign	128	
	b		irq_addr	/* IRQ/vIRQ */
	.balign	128
	b		fiq_addr	/* FIQ/vFIQ */
	.balign	128
	b		serr_addr	/* SError/vSError */
	.balign	128
	/* Current EL with SPx */
	b		sync_addr
	.balign	128
	b		irq_addr
	.balign	128
	b		fiq_addr
	.balign	128
	b		serr_addr
	.balign	128
	/* Lower EL using AArch64 */
	b		sync_addr
	.balign	128
	b		irq_addr
	.balign	128
	b		fiq_addr
	.balign	128
	b		serr_addr
	.balign	128
	/* Lower EL using AArch32 */
	b		sync_addr
	.balign	128
	b		irq_addr
	.balign	128
	b		fiq_addr
	.balign	128
	b		serr_addr

sync_addr:		.word reset_handler
irq_addr:       .word irq_handler
fiq_addr:       .word reset_handler
serr_addr:		.word reset_handler

_endvectors:

.section .text

/* el_setter: Sets processor to correct Exception Level (EL1)
 * for kernel operation. EL upon boot is EL2, while EL1 is necessary 
 * for kernel code */
el_setter:

/*
	// Enable EL2 access to timers 
	mrs	x0, cnthctl_el2
	orr	x0, x0, #0x3
	msr	cnthctl_el2, x0	

	// Enable EL1 access to timers 
	mrs	x0, cntkctl_el1
	orr	x0, x0, #0x3
	msr	cntkctl_el1, x0
*/
	/* Initialize MIDR/MPIDR registers */
//	mrs	x0, midr_el1
//	mrs	x1, mpidr_el1
//	msr	vpidr_el2, x0
//	msr	vmpidr_el2, x1

	/* Disable coprocessor traps */
//	mov	x0, #0x33FF
//	msr	cptr_el2, x0	/* Disable coprocessor traps to EL2 */
//	msr	hstr_el2, xzr	/* Disable Hyp System Trap */

	/* Enable FP/SIMD at EL1 */
	mov	x0, #3 << 20	/* .FPEN = 0b11, no instructions are trapped */
	msr cpacr_el1, x0

	/* Initialize HCR_EL2 */
	mov	x0, #(1 << 31)		/* 64-bit EL1 */
//	orr	x0, x0, #(1 << 29)	/* Disable HVC */
	/* Route interrupts to EL1 */
	bic x0, x0, #(1 << 5)	/* .AMO = 0b0 */
	bic x0, x0, #(1 << 4)	/* .IMO = 0b0 */
	bic x0, x0, #(1 << 3)	/* .FMO = 0b0 */
	bic	x0, x0, #(1 << 27)	/* .TGE = 0b0 */
	msr	hcr_el2, x0

	ldr	x0, =0x80000
	msr	vbar_el1, x0

	/* SCTLR_EL1 initialization */
	mov	x0, #0x0800
	movk x0, #0x30D0, lsl #16
	msr	sctlr_el1, x0

	/* Return to EL1_SP1 mode from EL2 */
	mov	x0, sp
	msr	sp_el1, x0		/* Migrate SP */
	
//	mrs	x0, vbar_el2
//	msr	vbar_el1, x0	/* Migrate VBAR */

	mov	x0, #0x3C5
	msr	spsr_el2, x0	/* EL1_SP1 | D | A | I | F */
	msr	elr_el2, x30

	eret
	
	/* reset_handler: Reset handler routine executed to start up the kernel,
	 * when the ARM processor is reset, or (currently) when an unhandled
	 * exception occurs.  */
	.func reset_handler
reset_handler:

	
/**
 * This function sets up the expected C environment and then runs the null
 * process.  A major part of the expected C environment is memory
 * preparation.  The standard XINU executable consists of three main
 * segments.  (In this context, a 'segment' is a relocatable section, not
 * necessarily corresponding to virtual memory segments.)  These are:
 *  Text segment (assembled machine instructions for the program code),
 *  Data segment (initialized data), and
 *  BSS ("Block Started by Symbol") segment (uninitialized data).
 * The Stack segment provides space for a stack of activation records
 *  used to implement statically-scoped variables and recursive procedure
 *  calls in most ALGOL-like languages, including C and all its progeny.
 * The BSS is the unitialized data segment produced by the Unix linkers.
 *  Objects in the BSS segment have only a name and a size, but no value.
 * In addition, the running program conceptually also has:
 *  Stack segment (stack of activation records,)
 *  Heap segment (area for dynamic memory allocation).
 *
 * Memory Layout (Not to scale)
 * +----------+
 * |          |
 * |          |
 * |   HEAP   | -> getmem allocates from here
 * |          |
 * |          |
 * +----------+
 * | OS STACK | -> becomes the null process' stack
 * +----------+
 * |   BSS    | -> needed for C environment
 * +----------+
 * |   TEXT   | -> XINU code
 * |          |
 * +----------+
 * | RESERVED | -> interrupt handler and vectors go here
 * +----------+
 */

	/* Clear the .bss section of the kernel.  */
	ldr x0, =_bss
	ldr x1, =_end
  	mov x2, #0
  	b bssloopa
bssloopb:
  	str x2, [x0, 8]
  	str x2, [x0, 16]
  	str x2, [x0, 24]
  	str x2, [x0, 32]
  	add x0, x0, 32

bssloopa:
	cmp x0, x1 	/* check that we still haven't hit the end of bss yet */
	b.lo bssloopb	/* if still below, go backwards and loop */



	/* Put the null thread's stack directly after the kernel image.  */
	add sp, x1, #NULLSTK

	/* The remaining memory available to the ARM will be Xinu's "memheap"
	 * region, which is used for dynamic memory allocation.  Set its
	 * starting address.  */
	ldr x0, =memheap
    mov x1, sp
    str x1, [x0]

	/* Put non-used cores in busy wait */
  	mrs x7, mpidr_el1
  	and x7, x7, #3
  	cbz x7, __start_master
0:  wfe
  	b   0b

	
	/* Branch to the C environment */
__start_master:
	b	nulluser
	.endfunc


.globl DOWFI
DOWFI:
	wfi
	ret

//.globl dmb

/**
 * @fn void dmb(void)
 *
 * Executes a data memory barrier operation using the c7 (Cache Operations)
 * register of system control coprocessor CP15.
 *
 * All explicit memory accesses occurring in program order before this operation
 * will be globally observed before any memory accesses occurring in program
 * order after this operation.  This includes both read and write accesses.
 *
 * This differs from a "data synchronization barrier" in that a data
 * synchronization barrier will ensure that all previous explicit memory
F
 * accesses occurring in program order have fully completed before continuing
 * and that no subsequent instructions will be executed until that point, even
 * if they do not access memory.  This is unnecessary for what we need this for.
 *
 * On the BCM2835 (Raspberry Pi), this is needed before and after accessing
 * peripherals, as documented on page 7 of the "BCM2835 ARM Peripherals"
 * document.  As documented, it is only needed when switching between
 * _different_ peripherals.
 */
/*
dmb:
	.func dmb
	mov	r12, #0
	mcr	p15, 0, r12, c7, c10, 5
	mov 	pc, lr
	.endfunc
*/
